resource "kubernetes_deployment" "nginx" {
  metadata {
    namespace = local.namespace
    name      = "nginx"
    labels = {
      App = "nginx"
    }
  }

  spec {
    replicas = 2
    selector {
      match_labels = {
        App = "nginx"
      }
    }
    template {
      metadata {
        labels = {
          App = "nginx"
        }
      }
      spec {
        container {
          image = "nginx:latest"
          name  = "nginx"

          port {
            name           = "web"
            container_port = 80
          }

          resources {
            limits = {
              cpu    = "0.5"
              memory = "512Mi"
            }
            requests = {
              cpu    = "250m"
              memory = "50Mi"
            }
          }
        }
      }
    }
  }
  depends_on = [helm_release.alb_controller]
}


#------------------------------------------------------------------------------
# Amazon EKS pods running on AWS Fargate now support custom security groups
# https://aws.amazon.com/about-aws/whats-new/2021/06/amazon-eks-pods-running-aws-fargate-support-custom-security-groups/
#
# https://github.com/weaveworks/kubernetesctl/issues/1640
# For exposing an HTTP service, it so far only supports ALB with IP mode and you should specify your service as ClusterIP.
#------------------------------------------------------------------------------
resource "kubernetes_service" "nginx" {
  metadata {
    name      = "ingress-service"
    namespace = local.namespace
    annotations = {
      "alb.ingress.kubernetes.io/target-type" = "ip"
    }
  }
  spec {
    type = "ClusterIP"
    selector = {
      App = kubernetes_deployment.nginx.spec.0.template.0.metadata[0].labels.App
    }
    port {
      port        = 80
      target_port = 80
      protocol    = "TCP"
    }

  }
  depends_on = [kubernetes_deployment.nginx]
}
